apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer
    defaultConfig {
        applicationId "com.rye.catcher.zzg"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 7
        versionName "1.0.7"

        ndk {
            abiFilter "armeabi-v7a"
        }
    }
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"   //这个改大
    }

    signingConfigs {//只是做个实例，默认的配置信息，对app进行签名
        config {
            storeFile file('../ryeCatcher.jks')
            storePassword 'catcher'
            keyAlias 'key0'
            keyPassword 'catcher'
        }
        //release{}   debug{}
    }

    buildTypes {
        release {
            shrinkResources true //用来开启删除无用资源，也就是没有被引用的文件,和底下这个要配合使用，只有全为true两者才起作用
            minifyEnabled true  //minifyEnabled主要用来控制是否运行混淆的，还有就是用来开启删除无用代码，比如没有引用到的代码
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config //签名不要忘了撒
            manifestPlaceholders = [appid: "100250345"] //貌似是华为沙盒用的，不用管
            zipAlignEnabled true //这个在打包时需要设置为true，能优化我们的java字节码，提高运行效率；
        }
        debug {//默认安装的文件

            def today = new Date()
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            signingConfig signingConfigs.config
            manifestPlaceholders = [appid: "100250711"]
            applicationIdSuffix '.anyOATest' //applicationIdSuffix代表了默认的应用ID
            versionNameSuffix '.' + today.format('MMdd')
            zipAlignEnabled false
        }

        preview {
            shrinkResources false //用来开启删除无用资源，也就是没有被引用的文件,和底下这个要配合使用，只有全为true两者才起作用
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            signingConfig signingConfigs.config //引用签名文件
            manifestPlaceholders = [appid: "100250711"]
            applicationIdSuffix '.anyOATest' //测试版id和生产版的不一样，一样的话就不能共存了呦
            versionNameSuffix '.01'   //版本号修改
            zipAlignEnabled true
            matchingFallbacks = ['debug', 'release']

        }
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }

    packagingOptions {//排除不想放入到apk文件里的文件
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
//维度，比如前面这个，场景：可以出一个免费版一个付费版，后面这个，场景：小米商城、华为商城
    //如果一个是vip，一个是free，这就不是两个维度了，因为这俩都跟money有关
    //和productFlavors配合使用
    flavorDimensions("money", "channel")
    productFlavors {
        vip {
            dimension "money"
        }
        free {
            dimension "money"
        }
        baidu {
            manifestPlaceholders = [CHANNEL: "baidu"]
            dimension "channel"
        }
        xiaomi {
            manifestPlaceholders = [CHANNEL: "xiaomi"]
            dimension "channel"
        }
    }
    //一个有用的技巧：你可以单独只为某一个variants添加一些依赖，只需要在Compile加上对应的variant名字前缀就可以了
    //dependencies {
    //compile 'com.android.support:support-v4:22.2.0'
    // vipCompile 'com.android.support:appcompat-v7:22.2.0'//只为branchOne添加这个依赖
//}

    lintOptions {//貌似是用来屏蔽lint错误，拼写错误?
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    greendao {
        schemaVersion 1
        daoPackage 'com.example.myappsecond.GreenDaos.Base'
        targetGenDir 'src/main/java'
    }
}
repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }//Toasty
}
android.applicationVariants.all { variant ->

    variant.outputs.all { output ->
        if (variant.buildType.name == "debug" || variant.buildType.name == "preview") {
            resValue "string", "ip_out", "http://114.255.225.50:8088"
            resValue "string", "ip_in", "http://122.40.130.74:8088"
            resValue "string", "app_name", "工银e办公测"
            resValue "string", "SCREENSHOT_ENANBLE", "1"
            resValue "string", "SANDBOX_WHITELIST", "122.40.130.74;122.18.157.193;122.19.141.46;83."
            resValue "string", "CAN_CHANGE_USER_CONFIG", "0"

            if (variant.buildType.name == "preview") {
                outputFileName = "${rootProject.name}-${variant.buildType.name}" +
                        "-${variant.versionName}.apk"
                variant.packageApplication.outputDirectory = new File("./../apk")
            }

        } else {
            resValue "string", "ip_out", "https://140.206.54.50:8080"
            resValue "string", "ip_in", "http://10.248.113.15:8081"
            resValue "string", "app_name", "工银e办公"
            resValue "string", "SCREENSHOT_ENANBLE", "0"
            resValue "string", "SANDBOX_WHITELIST", "10.248.113.15;122.19.141.46;biom-mbo;biom-" +
                    "mbo.icbc;gcms.gcms.icbc;cbms-web.icbc"
            resValue "string", "CAN_CHANGE_USER_CONFIG", "1"

            outputFileName = "${rootProject.name}-${variant.buildType.name}" +
                    "-${variant.versionName}.apk"
            variant.packageApplication.outputDirectory = new File("./../apk")
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    implementation files('libs/universal-image-loader-1.9.3.jar')
    implementation files('libs/universal-image-loader-1.9.3-sources.jar')
    implementation files('libs/AMap_Location_V4.1.0_20180619.jar')
    implementation files('libs/commons-io-2.4.jar')
    implementation files('libs/android-support-v7-recyclerview.jar')
    //Rxjava2
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    //greendao之在线引用
    implementation 'org.greenrobot:greendao:3.2.0'
    //OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.github.GrenderG:Toasty:1.2.5'
    //相应的project 的build.gradle中也有相关配置
    implementation project(':pulltorefresh')
    //Glide图片加载框架
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'
    //EventBus,被一亿多app使用
    implementation 'org.greenrobot:eventbus:3.0.0'
    //tencent bugly
    implementation 'com.tencent.bugly:crashreport:latest.release'
    //其中latest.release指代最新Bugly SDK版本号，也可以指定明确的版本号，例如2.1.9
    implementation 'com.tencent.bugly:nativecrashreport:latest.release'
    implementation 'com.github.bestTao:CoordinatorMenu:v1.0.2'
    //butterknife
    implementation 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
}