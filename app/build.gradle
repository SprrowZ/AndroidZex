apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply from: 'tinker-support.gradle'
android {
    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer
    defaultConfig {
        applicationId "com.rye.catcher.zzg"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 7
        versionName "1.0.7"
        //bugly需要，还有其他地方要用，架构模式
        ndk {
            abiFilter "armeabi-v7a"
        }
    }
    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    signingConfigs {
        config {
            storeFile file('../ryeCatcher.jks')
            storePassword 'catcher'
            keyAlias 'key0'
            keyPassword 'catcher'
        }
        //release{}   debug{}
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config //签名不要忘了撒
            manifestPlaceholders = ["appName": "RyeCatcher"] //manifest替换
            zipAlignEnabled true
        }
        debug {//默认安装的文件，可修改
            def today = new Date()
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            signingConfig signingConfigs.config
            manifestPlaceholders = ["appName": "麦田Z"]
            applicationIdSuffix '.anyOATest' //applicationIdSuffix代表了默认的应用ID
            versionNameSuffix '.' + today.format('MMdd')
            zipAlignEnabled false
        }

        preview {
            shrinkResources false //用来开启删除无用资源，也就是没有被引用的文件,和底下这个要配合使用，只有全为true两者才起作用
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            signingConfig signingConfigs.config
            manifestPlaceholders = ["appName": "RyeCatcherP"]
            applicationIdSuffix '.anyOATest'
            versionNameSuffix '.01'
            zipAlignEnabled true
            matchingFallbacks = ['debug', 'release']

        }
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }

    packagingOptions {//排除不想放入到apk文件里的文件
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

//    flavorDimensions("money", "channel")
//    productFlavors {
//        vip {
//            dimension "money"
//            buildConfigField("boolean", "IS_VIP_VERSION", "true")
//            minSdkVersion 16
//        }
//        free {
//            dimension "money"
//            buildConfigField("boolean", "IS_VIP_VERSION", "false")
//            minSdkVersion 16
//        }
//        baidu {
//            manifestPlaceholders = [CHANNEL: "baidu"]
//            dimension "channel"
//            buildConfigField("boolean", "IS_baidu_VERSION", "true")
//            minSdkVersion 16
//        }
//        xiaomi {
//            manifestPlaceholders = [CHANNEL: "xiaomi"]
//            dimension "channel"
//            buildConfigField("boolean", "IS_baidu_VERSION", "false")
//            minSdkVersion 16
//        }
//    }

    lintOptions {//貌似是用来屏蔽lint错误，拼写错误?
        checkReleaseBuilds false
        abortOnError false
    }

    greendao {
        schemaVersion 1
        daoPackage 'com.rye.catcher.GreenDaos.Base'
        targetGenDir 'src/main/java'
    }
}
repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }//Toasty
}
android.applicationVariants.all { variant ->

    variant.outputs.all { output ->
        if (variant.buildType.name == "debug" || variant.buildType.name == "preview") {
            resValue "string", "ip_out", "http://114.255.225.50:8088"
            resValue "string", "ip_in", "http://122.40.130.74:8088"
            resValue "string", "app_name", "寻梦测"
            resValue "string", "SCREENSHOT_ENANBLE", "1"
            resValue "string", "SANDBOX_WHITELIST", "122.40.130.74;122.18.157.193;122.19.141.46;83."
            resValue "string", "CAN_CHANGE_USER_CONFIG", "0"
            buildConfigField "String", "ip", "\"www.bilibili.com\""
            if (variant.buildType.name == "preview") {
                outputFileName = "${rootProject.name}-${variant.buildType.name}" +
                        "-${variant.versionName}.apk"
                variant.packageApplication.outputDirectory = new File("./../apk")
            }

        } else {
            resValue "string", "ip_out", "https://140.206.54.50:8080"
            resValue "string", "ip_in", "http://10.248.113.15:8081"
            resValue "string", "app_name", "寻梦"
            resValue "string", "SCREENSHOT_ENANBLE", "0"
            resValue "string", "SANDBOX_WHITELIST", "10.248.113.15;cbms-web.icbc"
            resValue "string", "CAN_CHANGE_USER_CONFIG", "1"
            buildConfigField "String", "ip", "\"www.bilibili.com\""
//            outputFileName = "${rootProject.name}-${variant.buildType.name}" +
//                    "-${variant.versionName}.apk"
//            variant.packageApplication.outputDirectory = new File("./../apk")
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-v4:27.1.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportVer"
    implementation "com.android.support:cardview-v7:$rootProject.ext.supportVer"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportVer"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:design:$rootProject.ext.supportVer"
    testImplementation 'junit:junit:4.12'
    implementation files('libs/commons-io-2.4.jar')
    implementation files('libs/IKAnalyzer2012_u6.jar')//sqlite全文检索；
    implementation files('libs/lucene-core-3.6.0.jar')//sqlite全文检索；
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'org.greenrobot:greendao:3.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation "com.squareup.okhttp3:okhttp:$rootProject.ext.okHttpVer"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.ext.okHttpVer"
    implementation 'com.squareup.retrofit2:converter-gson:2.0.2'
    implementation 'com.github.GrenderG:Toasty:1.2.5'
    implementation project(':pulltorefresh')
    implementation "com.github.bumptech.glide:glide:$rootProject.ext.glideVer"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.ext.glideVer"
    implementation 'org.greenrobot:eventbus:3.0.0'

    implementation "com.jakewharton:butterknife:$rootProject.ext.butterVer"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterVer"
    //6.0权限适配，机型适配
    implementation 'com.yanzhenjie:permission:2.0.0-rc11'
    implementation 'com.alibaba:fastjson:1.2.49'
    implementation 'com.amap.api:location:latest.integration'
    //bugly
    implementation "com.android.support:multidex:1.0.1" // 多dex配置
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.5'
    implementation 'com.tencent.tinker:tinker-android-lib:1.9.6'
    implementation 'com.tencent.bugly:nativecrashreport:latest.release' //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.2.0
}
